buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'java'

String libraryVersionStr = ext.libraryVersionStr = '0.5.0'

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }
    repositories {
        jcenter()
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"

publishing {
    publications {
        jarPublication(MavenPublication) {
            groupId 'info.vividcode.android'
            artifactId 'android-sdk-client'
            version libraryVersionStr
            artifacts = configurations.archives.allArtifacts

            // Add dependencies to pom.
            // See: http://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
            pom.withXml {
                //Creating additional node for dependencies
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    boolean isNotProjectDependency = !(it instanceof ProjectDependency)
                    if (it.group != null && it.name != null && isNotProjectDependency) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['jarPublication']
    pkg {
        repo = 'maven'
        name = 'android-sdk-client'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nobuoka/AndroidSdkClientForJava.git'
        version {
            name = libraryVersionStr
        }
    }
}

bintrayUpload.dependsOn(assemble)
